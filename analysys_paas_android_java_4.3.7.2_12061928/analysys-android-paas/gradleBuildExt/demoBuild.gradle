// demo相关公共配置
apply from: './gradleBuildExt/baseBuild.gradle'

ext {

    processors = [
            ARouter_compiler: 'com.alibaba:arouter-compiler:1.2.2',
    ]

    android_dependencies = [

            /*android库*/
            appcompat       : 'androidx.appcompat:appcompat:1.0.2',
            supportDesign   : "com.android.support:design:${buildToolsVer}",
            annotation      : 'androidx.annotation:annotation:1.0.2',
            /*android拓展库*/
            constraintLayout: 'androidx.constraintlayout:constraintlayout:1.1.3',
            lifecycle       : 'android.arch.lifecycle:extensions:1.1.1'
    ]

    /*三方库公用库*/
    other_dependencies = [
            // 路由实现方案
            ARouter: 'com.alibaba:arouter-api:1.5.0',
    ]


    gradle_base_demo = { projectImp, androidPlugin = 'com.android.library',
                         version_code = 1, version_name = "1.0" ->

        rootProject.ext.gradle_base(projectImp, androidPlugin)

        projectImp.android {
            defaultConfig {
                versionCode version_code
                versionName version_name
                multiDexEnabled true

                // ARouter
                javaCompileOptions {
                    annotationProcessorOptions {
                        arguments = [
                                AROUTER_MODULE_NAME  : projectImp.name,
                                "room.schemaLocation": "$projectDir/schemas".toString()
                        ]
                    }
                }


                // 默认配置
                manifestPlaceholders = [
                        GETUI_APP_ID    : "gI8n3bRHKt8RF1Ovovep57",
                        GETUI_APP_KEY   : "KLTj3f2BfE72zs3ggKhvb1",
                        GETUI_APP_SECRET: "fh978N75mK6SbqkWE4AB28",
                        JPUSH_PKGNAME   : "com.analysys.demo",
                        JPUSH_APPKEY    : "fb807356b9492193f5546006", //更新appkey
                        JPUSH_CHANNEL   : "developer-default" //暂时填写默认值即可.
                ]
            }

            dataBinding {
                enabled = true
            }
        }

        projectImp.dependencies {
            // 三方库
            implementation android_dependencies.values() + other_dependencies.values()
            annotationProcessor processors.values()

            // 测试库
            testImplementation 'junit:junit:4.12'
            androidTestImplementation 'com.android.support.test:runner:1.0.2'
            androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
        }
    }

}


